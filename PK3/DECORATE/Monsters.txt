ACTOR QuakeEnforcer
{
  Health 100
  Radius 20
  Height 56
  Mass 100
  Scale 0.7
  Speed 7
  PainChance 140
  Monster
  +FLOORCLIP
  SeeSound "enforcer/sight"
  PainSound "enforcer/pain"
  DeathSound "enforcer/death"
  ActiveSound "enforcer/active"
  Obituary "%o was lasered by an Enforcer"
  States
  {
  Spawn:
    NFRC AB 10 A_Look
    Loop
  See:
    NFRC ABCDEFGH 3 A_Chase
    Loop
  Missile:
    NFRC IJKL 2 A_FaceTarget
    NFRC M 5 Bright A_SpawnProjectile("EnforcerBolt", 34, 8)
    NFRC LKL 3 A_FaceTarget
    NFRC M 5 Bright A_SpawnProjectile("EnforcerBolt", 34, 8)
    NFRC LKJI 2
    Goto See
  Pain:
    NFRC O 3
    NFRC O 3 A_Pain
    Goto See
  Death:
    NFRC PQ 3
    NFRC R 3 A_Scream
    NFRC S 3 A_NoBlocking
    NFRC TUVWXYZ 3
    NFRC Z -1
    Stop
  XDeath:
    NFRC O 5
    SPOS N 5 A_XScream
    SPOS O 5 A_NoBlocking
    SPOS PQRST 5
    SPOS U -1
    Stop
  Raise:
    NFRC ZYXWVUTSRQP 2
    Goto See
  }
}

ACTOR EnforcerBolt
{
  Radius 3
  Height 4
  Scale 0.5
  Speed 20
  FastSpeed 30
  Damage 3
  Projectile
  +RANDOMIZE
  RenderStyle Add
  Alpha 1
  SeeSound "enforcer/attack"
  DeathSound "enforcer/stop"
  Translation "176:191=160:167", "208:223=231:228", "168:175=167:160", "232:235=164:167"
  States
  {
  Spawn:
    BAL1 AABB 1 Bright A_SpawnParticle("Yellow", SPF_FULLBRIGHT, random(35, 55), 3, 0, 0, 0, 0, 0, 0, -0.2)
    Loop
  Death:
    BAL1 CDE 6 Bright
    Stop
  }
}

ACTOR ImpKnight : DoomImp
{
  Translation "64:79=112:127"
  Health 250
  Scale 1.3
  States {
    Melee:
    Missile:
        TROO E 10 A_FaceTarget
        TROO F 4 A_SpawnProjectile("KnightBolt", 40, 16, -30)
        TROO F 4 A_SpawnProjectile("KnightBolt", 40, 8, -15)
        TROO G 4 A_SpawnProjectile("KnightBolt", 40, 0, 0)
        TROO G 4 A_SpawnProjectile("KnightBolt", 40, -8, 15)
        TROO G 4 A_SpawnProjectile("KnightBolt", 40, -16, 30)
        Goto See
  }
}

ACTOR KnightBolt
{
  Radius 3
  Height 4
  Scale 0.5
  Speed 7
  FastSpeed 10
  Damage (random(6, 10))
  Projectile
  +RANDOMIZE
  RenderStyle Add
  Alpha 1
  SeeSound "imp/attack"
  Translation "176:191=160:167", "208:223=231:228", "168:175=167:160", "232:235=164:167"
  States
  {
  Spawn:
    BAL1 A 1 Bright A_SpawnParticle("Yellow", SPF_FULLBRIGHT | SPF_RELANG | SPF_RELVEL, random(25, 65), 4, 0, 0, 0, 0, 0, 0.5, 0.2)
    BAL1 A 0 Bright A_SpawnParticle("Yellow", SPF_FULLBRIGHT | SPF_RELANG | SPF_RELVEL, random(25, 65), 4, 0, 0, 0, 0, 0, -0.5, 0.2)
    BAL1 B 1 Bright A_SpawnParticle("Yellow", SPF_FULLBRIGHT | SPF_RELANG | SPF_RELVEL, random(25, 65), 4, 0, 0, 0, 0, 0, 0.5, 0.2)
    BAL1 B 0 Bright A_SpawnParticle("Yellow", SPF_FULLBRIGHT | SPF_RELANG | SPF_RELVEL, random(25, 65), 4, 0, 0, 0, 0, 0, -0.5, 0.2)
    Loop
  Death:
    BAL1 CDE 6 Bright
    Stop
  }
}

ACTOR FlyingImp
{
  Translation "64:79=96:111", "176:191=240:247", "236:239=244:247", "32:47=240:247", "1:2=245:246"
  OBITUARY "%o was blasted by an flying imp."
  HITOBITUARY "%o was slashed by an flying imp."
  Health 80
  Radius 20
  Height 56
  Mass 100
  Speed 8
  PainChance 200
  MissileType "FlyingImpBall"
  SEESOUND "imp/sight"
  PAINSOUND "imp/pain"
  DEATHSOUND "imp/death"
  ACTIVESOUND "imp/active"
  MONSTER
  +Float
  +NoGravity
  states
  {
  Spawn:
    AEIM ABCD 10 A_Look
    Loop
  See:
    AEIM AABBCCDD 3 A_Chase
    Loop
  Missile:
    AEIM E 4 A_FaceTarget
    AEIM F 6 A_MissileAttack
    Goto See
  Pain:
    AEIM G 2
    AEIM G 2 A_Pain
    Goto See
  Death:
    AEIM H 8
    AEIM I 8 A_Scream
    AEIM J 6
    AEIM K 6 A_NoBlocking
    AEIM L 6
    AEIM M 6
    AEIM N -1
    Stop
  }
}

actor FlyingImpBall
{
  radius 6
  height 8
  speed 10
  damage 3
  renderstyle Add
  seesound "imp/attack"
  deathsound "imp/shotx"
  PROJECTILE
  +RANDOMIZE
  Decal DoomImpScorch
  states
  {
  Spawn:
    DRFX ABCDEF 4 bright
    loop
  Death:
    DRFX GHIJKL 6 bright
    stop
  }
}

actor QuakeGrunt : ShotgunGuy
{
    Translation "96:111=112:127", "32:47=121:127"
    Health 80
    Speed 3
    PainChance 250
    States {
        Missile:
            SPOS E 10 A_FaceTarget
            SPOS F 0 bright A_PlaySound("shotguy/attack", CHAN_WEAPON)
            SPOS F 10 bright A_CustomBulletAttack(22.5, 0, 3, random(1,4)*3, "BulletPuff", 0, CBAF_NORANDOM)
            SPOS E 10
            Goto See
    }
}
